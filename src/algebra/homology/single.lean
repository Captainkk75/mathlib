/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.homology.homology
import tactic.omega

/-!
# Chain complexes supported in a single degree

We define `single V j c : V ‚•§ homological_complex V c`,
which constructs complexes in `V` of shape `c`, supported in degree `j`.

Similarly `single_0 V : V ‚•§ chain_complex V ‚Ñï` is the special case for
`‚Ñï`-indexed chain complexes, with the object supported in degree `0`,
but with better definitional properties.

In `to_single_0_equiv` we characterize chain maps to a `‚Ñï`-indexed complex concentrated in degree 0;
they are equivalent to `{ f : C.X 0 ‚ü∂ X // C.d 1 0 ‚â´ f = 0 }`.
(This is useful translating between a projective resolution and
an augmented exact complex of projectives.)
-/

open category_theory
open category_theory.limits

universes v u

variables (V : Type u) [category.{v} V] [has_zero_morphisms V] [has_zero_object V]

namespace homological_complex
variables {Œπ : Type*} [decidable_eq Œπ] (c : complex_shape Œπ)

local attribute [instance] has_zero_object.has_zero

/--
The functor `V ‚•§ homological_complex V c` creating a chain complex supported in a single degree.

See also `chain_complex.single_0 : V ‚•§ chain_complex V ‚Ñï`,
which has better definitional properties,
if you are working with `‚Ñï`-indexed complexes.
-/
@[simps]
def single (j : Œπ) : V ‚•§ homological_complex V c :=
{ obj := Œª A,
  { X := Œª i, if i = j then A else 0,
    d := Œª i j, 0, },
  map := Œª A B f,
  { f := Œª i, if h : i = j then
      eq_to_hom (by { dsimp, rw if_pos h, }) ‚â´ f ‚â´ eq_to_hom (by { dsimp, rw if_pos h, })
    else
      0, },
  map_id' := Œª A, begin
    ext,
    dsimp,
    split_ifs with h,
    { subst h, simp, },
    { rw if_neg h, simp, },
  end,
  map_comp' := Œª A B C f g, begin
    ext,
    dsimp,
    split_ifs with h,
    { subst h, simp, },
    { simp, },
  end, }.

@[simps]
def single_obj_X_self (j : Œπ) (A : V) : ((single V c j).obj A).X j ‚âÖ A :=
eq_to_iso (by simp)

@[simp]
lemma single_map_f_self (j : Œπ) {A B : V} (f : A ‚ü∂ B) :
  ((single V c j).map f).f j =
    (single_obj_X_self V c j A).hom ‚â´ f ‚â´ (single_obj_X_self V c j B).inv :=
by { simp, refl, }

end homological_complex

open homological_complex

namespace chain_complex

local attribute [instance] has_zero_object.has_zero

/--
`chain_complex.single_0 V` is the embedding of `V` into `chain_complex V ‚Ñï`
as chain complexes supported in degree 0.

This is naturally isomorphism to `single V _ 0`, but has better definitional properties.
-/
def single_0 : V ‚•§ chain_complex V ‚Ñï :=
{ obj := Œª X,
  { X := Œª n, match n with
    | 0 := X
    | (n+1) := 0
    end,
    d := Œª i j, 0, },
  map := Œª X Y f,
  { f := Œª n, match n with
    | 0 := f
    | (n+1) := 0
    end, },
  map_id' := Œª X, by { ext n, cases n, refl, dsimp, unfold_aux, simp, },
  map_comp' := Œª X Y Z f g, by { ext n, cases n, refl, dsimp, unfold_aux, simp, } }

@[simp] lemma single_0_obj_X_0 (X : V) : ((single_0 V).obj X).X 0 = X := rfl
@[simp] lemma single_0_obj_X_succ (X : V) (n : ‚Ñï) : ((single_0 V).obj X).X (n+1) = 0 := rfl
@[simp] lemma single_0_obj_X_d (X : V) (i j : ‚Ñï) : ((single_0 V).obj X).d i j = 0 := rfl
@[simp] lemma single_0_obj_X_d_to (X : V) (j : ‚Ñï) : ((single_0 V).obj X).d_to j = 0 :=
by { rw [d_to_eq ((single_0 V).obj X) rfl], simp, }
@[simp] lemma single_0_obj_X_d_from (X : V) (i : ‚Ñï) : ((single_0 V).obj X).d_from i = 0 :=
begin
  cases i,
  { rw [d_from_eq_zero], simp, },
  { rw [d_from_eq ((single_0 V).obj X) rfl], simp, },
end
@[simp] lemma single_0_map_f_0 {X Y : V} (f : X ‚ü∂ Y) : ((single_0 V).map f).f 0 = f := rfl
@[simp] lemma single_0_map_f_succ {X Y : V} (f : X ‚ü∂ Y) (n : ‚Ñï) :
  ((single_0 V).map f).f (n+1) = 0 := rfl

section
variables [has_equalizers V] [has_cokernels V] [has_images V] [has_image_maps V]

/--
Sending objects to chain complexes supported at `0` then taking `0`-th homology
is the same as doing nothing.
-/
noncomputable
def homology_functor_0_single_0 : single_0 V ‚ãô homology_functor V _ 0 ‚âÖ (ùü≠ V) :=
nat_iso.of_components (Œª X, homology.congr _ _ (by simp) (by simp) ‚â™‚â´ homology_zero_zero)
  (Œª X Y f, by { ext, dsimp [homology_functor], simp, })

/--
Sending objects to chain complexes supported at `0` then taking `(n+1)`-st homology
is the same as the zero functor.
-/
noncomputable
def homology_functor_succ_single_0 (n : ‚Ñï) : single_0 V ‚ãô homology_functor V _ (n+1) ‚âÖ 0 :=
nat_iso.of_components (Œª X, homology.congr _ _ (by simp) (by simp) ‚â™‚â´ homology_zero_zero)
  (Œª X Y f, by ext)

end

variables {V}

/--
Morphisms from a `‚Ñï`-indexed chain complex `C`
to a single object chain complex with `X` concentrated in degree 0
are the same as morphisms `f : C.X 0 ‚ü∂ X` such that `C.d 1 0 ‚â´ f = 0`.
-/
def to_single_0_equiv (C : chain_complex V ‚Ñï) (X : V) :
  (C ‚ü∂ (single_0 V).obj X) ‚âÉ { f : C.X 0 ‚ü∂ X // C.d 1 0 ‚â´ f = 0 } :=
{ to_fun := Œª f, ‚ü®f.f 0, by { rw ‚Üêf.comm 1 0, simp, }‚ü©,
  inv_fun := Œª f,
  { f := Œª i, match i with
    | 0 := f.1
    | (n+1) := 0
    end,
    comm' := Œª i j, begin
      rcases i with _|_|i; cases j; unfold_aux; simp only [comp_zero, zero_comp, single_0_obj_X_d],
      { rw [C.shape, zero_comp], simp, },
      { exact f.2.symm, },
      { rw [C.shape, zero_comp], simp only [complex_shape.down_rel, zero_add], omega, },
    end, },
  left_inv := Œª f, begin
    ext i,
    rcases i,
    { refl, },
    { ext, },
  end,
  right_inv := by tidy, }

end chain_complex
