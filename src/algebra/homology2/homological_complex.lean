import algebra.homology2.complex_shape
import category_theory.limits.shapes.zero
import category_theory.subobject.lattice
import category_theory.subobject.factor_thru

universes v u

open category_theory category_theory.limits

variables {Œπ : Type*}
variables (V : Type u) [category.{v} V] [has_zero_morphisms V]

structure homological_complex (c : complex_shape Œπ) :=
(X : Œπ ‚Üí V)
(d : Œ† i j, X i ‚ü∂ X j)
(shape' : ‚àÄ i j, ¬¨ c.r i j ‚Üí d i j = 0 . obviously)
(d_comp_d' : ‚àÄ i j k, d i j ‚â´ d j k = 0 . obviously)

restate_axiom homological_complex.shape'
restate_axiom homological_complex.d_comp_d'

attribute [simp] homological_complex.shape
attribute [simp, reassoc] homological_complex.d_comp_d

namespace homological_complex
variables {V} {c : complex_shape Œπ} (C : homological_complex V c)

@[ext] structure hom (A B : homological_complex V c) :=
(f : ‚àÄ i, A.X i ‚ü∂ B.X i)
(comm' : ‚àÄ i j, f i ‚â´ B.d i j = A.d i j ‚â´ f j . obviously)

restate_axiom hom.comm'
attribute [simp, reassoc] hom.comm

@[simps] def id (A : homological_complex V c) : hom A A :=
{ f := Œª _, ùüô _ }

@[simps] def comp (A B C : homological_complex V c) (œÜ : hom A B) (œà : hom B C) : hom A C :=
{ f := Œª i, œÜ.f i ‚â´ œà.f i }

instance : category (homological_complex V c) :=
{ hom := hom,
  id := id,
  comp := comp, }

open_locale classical
noncomputable theory

variables [has_zero_object V]
local attribute [instance] has_zero_object.has_zero

lemma d_comp_eq_to_hom {i j j' : Œπ} (rij : c.r i j) (rij' : c.r i j') :
  C.d i j' ‚â´ eq_to_hom (congr_arg C.X (c.succ_eq rij' rij)) = C.d i j :=
begin
  have P : ‚àÄ h : j' = j, C.d i j' ‚â´ eq_to_hom (congr_arg C.X h) = C.d i j,
  { rintro rfl, simp, },
  apply P,
end

lemma eq_to_hom_comp_d {i i' j : Œπ} (rij : c.r i j) (rij' : c.r i' j) :
  eq_to_hom (congr_arg C.X (c.pred_eq rij rij')) ‚â´ C.d i' j = C.d i j :=
begin
  have P : ‚àÄ h : i = i', eq_to_hom (congr_arg C.X h) ‚â´ C.d i' j = C.d i j,
  { rintro rfl, simp, },
  apply P,
end

lemma kernel_eq_kernel [has_kernels V] {i j j' : Œπ} (r : c.r i j) (r' : c.r i j') :
  kernel_subobject (C.d i j) = kernel_subobject (C.d i j') :=
begin
  rw ‚Üêd_comp_eq_to_hom C r r',
  apply kernel_subobject_comp_iso,
end

lemma image_eq_image [has_images V] [has_equalizers V] [has_zero_object V]
  {i i' j : Œπ} (r : c.r i j) (r' : c.r i' j) :
  image_subobject (C.d i j) = image_subobject (C.d i' j) :=
begin
  rw ‚Üêeq_to_hom_comp_d C r r',
  apply image_subobject_iso_comp,
end

/--
The differential mapping into `C.X j`, or zero if there isn't one.

We represent this as an object over `C.X j`,
in order to avoid explicitly picking a predecessor of `j`,
and because the source might be `0`, rather than some `C.X i`.
-/
def d_to (j : Œπ) : over (C.X j) :=
if h : nonempty (c.pred j) then
  over.mk (C.d h.some.1 j)
else
  over.mk (0 : 0 ‚ü∂ C.X j)

/--
The differential mapping out of `C.X i`, or zero if there isn't one.

We represent this as an object under `C.X i`,
in order to avoid explicitly picking a successor of `i`,
and because the target might be `0`, rather than some `C.X j`.
-/
def d_from (i : Œπ) : under (C.X i) :=
if h : nonempty (c.succ i) then
  under.mk (C.d i h.some.1)
else
  under.mk (0 : C.X i ‚ü∂ 0)

lemma over.hom_congr {X : V} {A B : over X} (h : A = B) : A.hom = eq_to_hom (by subst h) ‚â´ B.hom :=
by { subst h, simp }

def d_to_left {i j : Œπ} (r : c.r i j) :
  (C.d_to j).left ‚âÖ C.X i :=
begin
  dsimp [d_to],
  apply eq_to_iso,
  rw [dif_pos (c.nonempty_pred r)],
  exact congr_arg C.X (c.nonempty_pred_some r),
end

lemma d_to_hom {i j : Œπ} (r : c.r i j) :
  (C.d_to j).hom = (C.d_to_left r).hom ‚â´ C.d i j :=
begin
  dsimp [d_to, d_to_left],
  convert over.hom_congr (dif_pos (c.nonempty_pred r)),
  repeat { apply (c.nonempty_pred_some r).symm, },
  repeat { assumption, },
end

lemma d_to_hom_eq_zero {j : Œπ} (h : c.pred j ‚Üí false) :
  (C.d_to j).hom = 0 :=
begin
  dsimp [d_to],
  rw [dif_neg],
  { refl, },
  { rintro ‚ü®a‚ü©, exact h a, }
end

lemma under.hom_congr {X : V} {A B : under X} (h : A = B) : A.hom = B.hom ‚â´ eq_to_hom (by subst h) :=
by { subst h, simp }

def d_from_right {i j : Œπ} (r : c.r i j) :
  (C.d_from i).right ‚âÖ C.X j :=
begin
  dsimp [d_from],
  apply eq_to_iso,
  rw [dif_pos (c.nonempty_succ r)],
  exact congr_arg C.X (c.nonempty_succ_some r),
end

lemma d_from_hom {i j : Œπ} (r : c.r i j) :
  (C.d_from i).hom = C.d i j ‚â´ (C.d_from_right r).inv :=
begin
  dsimp [d_from, d_from_right],
  convert under.hom_congr (dif_pos (c.nonempty_succ r)),
  repeat { apply (c.nonempty_succ_some r).symm, },
  repeat { assumption, },
end

lemma d_from_hom_eq_zero {i : Œπ} (h : c.succ i ‚Üí false) :
  (C.d_from i).hom = 0 :=
begin
  dsimp [d_from],
  rw [dif_neg],
  { refl, },
  { rintro ‚ü®a‚ü©, exact h a, }
end

@[simp]
lemma d_to_comp_d_from (j : Œπ) : (C.d_to j).hom ‚â´ (C.d_from j).hom = 0 :=
begin
  by_cases h : nonempty (c.pred j),
  { obtain ‚ü®‚ü®i, rij‚ü©‚ü© := h,
    by_cases h' : nonempty (c.succ j),
    { obtain ‚ü®‚ü®k, rjk‚ü©‚ü© := h',
      rw [C.d_to_hom rij, C.d_from_hom rjk],
      simp, },
    { rw [C.d_from_hom_eq_zero (not_nonempty_iff_imp_false.mp h')],
      simp, }, },
  { rw [C.d_to_hom_eq_zero (not_nonempty_iff_imp_false.mp h)],
    simp, },
end

lemma kernel_from_eq_kernel [has_kernels V] {i j : Œπ} (r : c.r i j) :
  kernel_subobject (C.d_from i).hom = kernel_subobject (C.d i j) :=
begin
  rw C.d_from_hom r,
  apply kernel_subobject_comp_iso,
end

lemma image_to_eq_image [has_images V] [has_equalizers V] [has_zero_object V]
  {i j : Œπ} (r : c.r i j) :
  image_subobject (C.d_to j).hom = image_subobject (C.d i j) :=
begin
  rw C.d_to_hom r,
  apply image_subobject_iso_comp,
end

end homological_complex
