/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.preadditive.projective
import algebra.homology.single
import algebra.homology.quasi_iso
import algebra.homology.homotopy_category
import algebra.homology.augment

noncomputable theory

open category_theory
open category_theory.limits

universes v u

namespace category_theory
variables {C : Type u} [category.{v} C]

open projective

section
variables [has_zero_morphisms C] [has_equalizers C] [has_images C]

@[nolint has_inhabited_instance]
structure ProjectiveResolution_core (Z : C) :=
(X : ‚Ñï ‚Üí C)
(d : Œ† n, X (n+1) ‚ü∂ X n)
(zero : X 0 ‚âÖ Z)
(projective : ‚àÄ n, projective (X (n+1)))
(epi : epi (d 0))
(exact : ‚àÄ n, exact (d (n+1)) (d n))

variables [has_zero_object C]

/--
A `ProjectiveResolution Z` consists of a bundled `‚Ñï`-indexed chain complex of projective objects,
along with a quasi-isomorphism to the complex consisting of just `Z` supported in degree `0`.

(We don't actually ask here that the chain map is a quasi-iso, just exactness everywhere:
that `œÄ` is a quasi-iso is a lemma when the category is abelian.
Should we just ask for it here?)

Except in situations where you want to provide a particular projective resolution
(for example to compute a derived functor),
you will not typically need to use this bundled object, and will instead use
* `projective_resolution Z`: the `‚Ñï`-indexed chain complex
  (equipped with `projective` and `exact` instances)
* `projective_resolution.œÄ Z`: the chain map from `projective_resolution Z` to
  `(single C _ 0).obj Z` (which comes equipped with appropriate `quasi_iso` and `epi` instances)
-/
@[nolint has_inhabited_instance]
structure ProjectiveResolution (Z : C) :=
(complex : chain_complex C ‚Ñï)
(œÄ : homological_complex.hom complex ((chain_complex.single_0 C).obj Z))
(projective : ‚àÄ n, projective (complex.X n) . tactic.apply_instance)
(exact‚ÇÄ : exact (complex.d 1 0) (œÄ.f 0) . tactic.apply_instance)
(exact : ‚àÄ n, exact (complex.d (n+2) (n+1)) (complex.d (n+1) n) . tactic.apply_instance)
(epi : epi (œÄ.f 0) . tactic.apply_instance)

attribute [instance] ProjectiveResolution.projective ProjectiveResolution.exact‚ÇÄ
  ProjectiveResolution.exact ProjectiveResolution.epi

def ProjectiveResolution.of_core {Z : C} (P : ProjectiveResolution_core Z) : ProjectiveResolution Z :=
let E := (chain_complex.of P.X P.d (Œª n, (P.exact n).w)) in
{ complex := chain_complex.truncate.obj E,
  œÄ := E.truncate_to ‚â´ ((chain_complex.single_0 C).map_iso P.zero).hom,
  projective := P.projective,
  exact‚ÇÄ := begin
    dsimp [chain_complex.truncate_to, chain_complex.to_single_0_equiv],
    simp only [category_theory.exact_comp_iso, E, chain_complex.of_d],
    exact P.exact 0,
  end,
  exact := Œª n, begin
    dsimp [chain_complex.truncate_to, chain_complex.to_single_0_equiv],
    simp only [category_theory.exact_comp_iso, E, chain_complex.of_d],
    exact P.exact (n+1),
  end,
  epi := begin
    dsimp [chain_complex.truncate_to, chain_complex.to_single_0_equiv],
    simp only [chain_complex.of_d],
    haveI := P.epi,
    apply epi_comp,
  end, }

/--
An object admits a projective resolution.
-/
class has_projective_resolution (Z : C) : Prop :=
(out [] : nonempty (ProjectiveResolution Z))

section
variables (C)

/--
You will rarely use this typeclass directly: it is implied by the combination
`[enough_projectives C]` and `[abelian C]`.
-/
class has_projective_resolutions : Prop :=
(out : ‚àÄ Z : C, has_projective_resolution Z)

attribute [instance] has_projective_resolutions.out

end

namespace ProjectiveResolution

variables [has_zero_object C] [has_image_maps C] [has_cokernels C]

@[simp] lemma œÄ_f_succ {Z : C} (P : ProjectiveResolution Z) (n : ‚Ñï) :
  P.œÄ.f (n+1) = 0 :=
begin
  apply zero_of_target_iso_zero,
  dsimp, refl,
end

instance {Z : C} (P : ProjectiveResolution Z) (n : ‚Ñï) : category_theory.epi (P.œÄ.f n) :=
by cases n; apply_instance

/-- A projective object admits a trivial projective resolution: itself in degree 0. -/
def self (Z : C) [category_theory.projective Z] : ProjectiveResolution Z :=
{ complex := (chain_complex.single_0 C).obj Z,
  œÄ := ùüô ((chain_complex.single_0 C).obj Z),
  projective := Œª n, begin
    cases n,
    { dsimp, apply_instance, },
    { dsimp, apply_instance, },
  end,
  exact‚ÇÄ := by { dsimp, apply_instance, },
  exact := Œª n, by { dsimp, apply_instance, },
  epi := by { dsimp, apply_instance, }, }

-- TODO define `ProjectiveResolution.self Z` for `[projective Z]`.

/- Auxiliary construction for `lift`. -/
def lift_f_zero {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
  P.complex.X 0 ‚ü∂ Q.complex.X 0 :=
factor_thru (P.œÄ.f 0 ‚â´ f) (Q.œÄ.f 0)

local attribute [instance] epi_comp

/- Auxiliary construction for `lift`. -/
def lift_f_one {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
  P.complex.X 1 ‚ü∂ Q.complex.X 1 :=
exact.lift (P.complex.d 1 0 ‚â´ lift_f_zero f P Q) (Q.complex.d 1 0) (Q.œÄ.f 0) (by simp [lift_f_zero])

/- Auxiliary lemma for `lift`. -/
@[simp] lemma lift_f_one_zero_comm
  {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
  lift_f_one f P Q ‚â´ Q.complex.d 1 0 = P.complex.d 1 0 ‚â´ lift_f_zero f P Q :=
begin
  dsimp [lift_f_zero, lift_f_one],
  simp,
end

/- Auxiliary construction for `lift`. -/
def lift_f_succ {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z)
  (n : ‚Ñï) (g : P.complex.X n ‚ü∂ Q.complex.X n) (g' : P.complex.X (n+1) ‚ü∂ Q.complex.X (n+1))
  (w : g' ‚â´ Q.complex.d (n+1) n = P.complex.d (n+1) n ‚â´ g) :
  Œ£' g'' : P.complex.X (n+2) ‚ü∂ Q.complex.X (n+2),
    g'' ‚â´ Q.complex.d (n+2) (n+1) = P.complex.d (n+2) (n+1) ‚â´ g' :=
‚ü®exact.lift
  (P.complex.d (n+2) (n+1) ‚â´ g') ((Q.complex.d (n+2) (n+1))) (Q.complex.d (n+1) n) (by simp [w]),
  (by simp)‚ü©

/-- A morphism in `C` lifts to a chain map between projective resolutions. -/
def lift {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
  P.complex ‚ü∂ Q.complex :=
begin
  fapply chain_complex.mk_hom,
  apply lift_f_zero f,
  apply lift_f_one f,
  apply lift_f_one_zero_comm f,
  rintro n ‚ü®g, g', w‚ü©,
  exact lift_f_succ f P Q n g g' w,
end

/-- The resolution maps interwine the lift of a morphism and that morphism. -/
@[simp, reassoc]
lemma lift_commutes
  {Y Z : C} (f : Y ‚ü∂ Z) (P : ProjectiveResolution Y) (Q : ProjectiveResolution Z) :
  lift f P Q ‚â´ Q.œÄ = P.œÄ ‚â´ (chain_complex.single_0 C).map f :=
begin
  ext n,
  rcases n with (_|_|n),
  { dsimp [lift, lift_f_zero], simp, },
  { dsimp [lift, lift_f_one], simp, },
  { dsimp, simp, },
end

-- Now that we've checked this property of the lift,
-- we can seal away the actual definition.
attribute [irreducible] lift

end ProjectiveResolution

end

namespace ProjectiveResolution

variables [preadditive C] [has_equalizers C] [has_images C] [has_image_maps C]
  [has_zero_object C] [has_cokernels C]

/-- An auxiliary definition for `lift_homotopy_zero`. -/
def lift_homotopy_zero_zero {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
  (f : P.complex ‚ü∂ Q.complex)
  (comm : f ‚â´ Q.œÄ = 0) : P.complex.X 0 ‚ü∂ Q.complex.X 1 :=
exact.lift (f.f 0) (Q.complex.d 1 0) (Q.œÄ.f 0)
  (congr_fun (congr_arg homological_complex.hom.f comm) 0)

/-- An auxiliary definition for `lift_homotopy_zero`. -/
def lift_homotopy_zero_one {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
  (f : P.complex ‚ü∂ Q.complex)
  (comm : f ‚â´ Q.œÄ = 0) : P.complex.X 1 ‚ü∂ Q.complex.X 2 :=
exact.lift
  (f.f 1 - P.complex.d 1 0 ‚â´ lift_homotopy_zero_zero f comm) (Q.complex.d 2 1) (Q.complex.d 1 0)
  (by simp [lift_homotopy_zero_zero])

/-- An auxiliary definition for `lift_homotopy_zero`. -/
def lift_homotopy_zero_succ {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
  (f : P.complex ‚ü∂ Q.complex)
  (comm : f ‚â´ Q.œÄ = 0) (n : ‚Ñï)
  (g : P.complex.X n ‚ü∂ Q.complex.X (n + 1)) (g' : P.complex.X (n + 1) ‚ü∂ Q.complex.X (n + 2))
  (w : f.f (n + 1) = g' ‚â´ Q.complex.d (n + 2) (n + 1) + P.complex.d (n + 1) n ‚â´ g) :
  P.complex.X (n + 2) ‚ü∂ Q.complex.X (n + 3) :=
exact.lift
  (f.f (n+2) - P.complex.d (n+2) (n+1) ‚â´ g') (Q.complex.d (n+3) (n+2)) (Q.complex.d (n+2) (n+1))
  (by simp [w])

/-- Any lift of the zero morphism is homotopic to zero. -/
def lift_homotopy_zero {Y Z : C} {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
  (f : P.complex ‚ü∂ Q.complex)
  (comm : f ‚â´ Q.œÄ = 0) :
  homotopy f 0 :=
begin
  fapply homotopy.mk_inductive,
  { exact lift_homotopy_zero_zero f comm, },
  { simp [lift_homotopy_zero_zero], },
  { exact lift_homotopy_zero_one f comm, },
  { simp [lift_homotopy_zero_one], },
  { rintro n ‚ü®g, g', w‚ü©,
    fsplit,
    { exact lift_homotopy_zero_succ f comm n g g' w, },
    { simp [lift_homotopy_zero_succ, w], }, }
end

/-- Two lifts of the same morphism are homotopic. -/
def lift_homotopy {Y Z : C} (f : Y ‚ü∂ Z) {P : ProjectiveResolution Y} {Q : ProjectiveResolution Z}
  (g h : P.complex ‚ü∂ Q.complex)
  (g_comm : g ‚â´ Q.œÄ = P.œÄ ‚â´ (chain_complex.single_0 C).map f)
  (h_comm : h ‚â´ Q.œÄ = P.œÄ ‚â´ (chain_complex.single_0 C).map f) :
  homotopy g h :=
begin
  apply homotopy.equiv_sub_zero.inv_fun,
  apply lift_homotopy_zero,
  simp [g_comm, h_comm],
end

/-- The lift of the identity morphism is homotopic to the identity chain map. -/
def lift_id_homotopy (X : C) (P : ProjectiveResolution X) :
  homotopy (lift (ùüô X) P P) (ùüô P.complex) :=
by { apply lift_homotopy (ùüô X); simp, }

/-- The lift of a composition is homotopic to the composition of the lifts. -/
def lift_comp_homotopy {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
  (P : ProjectiveResolution X) (Q : ProjectiveResolution Y) (R : ProjectiveResolution Z) :
  homotopy (lift (f ‚â´ g) P R) (lift f P Q ‚â´ lift g Q R) :=
by { apply lift_homotopy (f ‚â´ g); simp, }

/-- Any two projective resolutions are homotopy equivalent. -/
def homotopy_equiv {X : C} (P Q : ProjectiveResolution X) :
  homotopy_equiv P.complex Q.complex :=
{ hom := lift (ùüô X) P Q,
  inv := lift (ùüô X) Q P,
  homotopy_hom_inv_id := begin
    refine (lift_comp_homotopy (ùüô X) (ùüô X) P Q P).symm.trans _,
    simp [category.id_comp],
    apply lift_id_homotopy,
  end,
  homotopy_inv_hom_id := begin
    refine (lift_comp_homotopy (ùüô X) (ùüô X) Q P Q).symm.trans _,
    simp [category.id_comp],
    apply lift_id_homotopy,
  end, }

@[simp, reassoc] lemma homotopy_equiv_hom_œÄ {X : C} (P Q : ProjectiveResolution X) :
  (homotopy_equiv P Q).hom ‚â´ Q.œÄ = P.œÄ :=
by simp [homotopy_equiv]

@[simp, reassoc] lemma homotopy_equiv_inv_œÄ {X : C} (P Q : ProjectiveResolution X) :
  (homotopy_equiv P Q).inv ‚â´ P.œÄ = Q.œÄ :=
by simp [homotopy_equiv]

end ProjectiveResolution

section

variables [has_zero_morphisms C] [has_zero_object C] [has_equalizers C] [has_cokernels C]
  [has_images C] [has_image_maps C]

/-- An arbitrarily chosen projective resolution of an object. -/
abbreviation projective_resolution (Z : C) [has_projective_resolution Z] : chain_complex C ‚Ñï :=
(has_projective_resolution.out Z).some.complex

/-- The chain map from the arbitrarily chosen projective resolution `projective_resolution Z`
back to the chain complex consisting of `Z` supported in degree `0`. -/
abbreviation projective_resolution.œÄ (Z : C) [has_projective_resolution Z] :
  projective_resolution Z ‚ü∂ (chain_complex.single_0 C).obj Z :=
(has_projective_resolution.out Z).some.œÄ

/-- The lift of a morphism to a chain map between the arbitrarily chosen projective resolutions. -/
abbreviation projective_resolution.lift {X Y : C} (f : X ‚ü∂ Y)
  [has_projective_resolution X] [has_projective_resolution Y] :
  projective_resolution X ‚ü∂ projective_resolution Y :=
ProjectiveResolution.lift f _ _

end

variables (C) [preadditive C] [has_zero_object C] [has_equalizers C] [has_cokernels C]
  [has_images C] [has_image_maps C] [has_projective_resolutions C]

/--
Taking projective resolutions is functorial,
if considered with target the homotopy category
(`‚Ñï`-indexed chain complexes and chain maps up to homotopy).
-/
def projective_resolutions : C ‚•§ homotopy_category C (complex_shape.down ‚Ñï) :=
{ obj := Œª X, (homotopy_category.quotient _ _).obj (projective_resolution X),
  map := Œª X Y f, (homotopy_category.quotient _ _).map (projective_resolution.lift f),
  map_id' := Œª X, begin
    rw ‚Üê(homotopy_category.quotient _ _).map_id,
    apply homotopy_category.eq_of_homotopy,
    apply ProjectiveResolution.lift_id_homotopy,
  end,
  map_comp' := Œª X Y Z f g, begin
    rw ‚Üê(homotopy_category.quotient _ _).map_comp,
    apply homotopy_category.eq_of_homotopy,
    apply ProjectiveResolution.lift_comp_homotopy,
  end, }

end category_theory
