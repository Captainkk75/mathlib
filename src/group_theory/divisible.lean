/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import group_theory.subgroup.pointwise
import group_theory.quotient_group
import data.real.basic
import data.complex.basic

/-!
# Divisible Group

An abelian group `A` is said to be divisible iff for all `n ‚â† 0` `y ‚àà A`, there is an `x ‚àà A`
such that `n ‚Ä¢ x = y`. In this file, we adpot a constructive approach, i.e. we ask for an explicit
`div_int : A ‚Üí ‚Ñ§ ‚Üí A` function such that `div_int a 0 = 0` and `n ‚Ä¢ div_int a n = a` for `n ‚â† 0`.

## Main results
* `add_comm_group.divisible_of_smul_surj` : the constructive definition of divisiblity is implied by
  the condition that `n ‚Ä¢ x = a` has solutions for all `n ‚â† 0` and `a ‚àà A`.
* `add_comm_group.smul_surj_of_divisible` : the constructive definition of divisiblity implies
  the condition that `n ‚Ä¢ x = a` has solutions for all `n ‚â† 0` and `a ‚àà A`.
* `add_comm_group.divisible_of_smul_top_eq_top` : the constructive definition of divisiblity is
  implied by the condition that `n ‚Ä¢ A = A` for all `n ‚â† 0`.
* `add_comm_group.smul_top_eq_top_of_divisible` : the constructive definition of divisiblity implies
  the condition that `n ‚Ä¢ A = A` for all `n ‚â† 0`.
* `add_comm_group.divisible_prod` : `A √ó B` is divisible for any divisible `A` and `B`.
* `add_comm_group.divisible_pi` : Any product of divisble group is divisible.
* `add_comm_group.divisible_of_char_zero` : Any field of characteristic zero is divisible.
* `add_comm_group.divisible_quotient` : Quotient group of divisible group is divisible.
* `add_comm_group.divisible_of_surj` : if `A` is divisible and `A ‚Üí+ B` is surjective, then `B` is
  divisible.
TODO: Show that divisibility implies injectivity in the category of `AddCommGroup`.
-/

namespace add_monoid

variables (A Œ± : Type*) [add_monoid A] [has_scalar Œ± A] [has_zero Œ±]

/--
An `add_monoid A` is `Œ±`-divisible iff `n ‚Ä¢ x = a` has a solution for all `n ‚â† 0 ‚àà Œ±` and `a ‚àà A`.
Here we adpot a constructive approach where we ask an explicit `div : A ‚Üí Œ± ‚Üí A` function such that
* `div a 0 = 0` for all `a ‚àà A`
* `n ‚Ä¢ div a n = a` for all `n ‚â† 0 ‚àà Œ±` and `a ‚àà A`.
-/
class divisible_by :=
(div : A ‚Üí Œ± ‚Üí A)
(div_zero : ‚àÄ a, div a 0 = 0)
(div_cancel : ‚àÄ {n : Œ±} (a : A), n ‚â† 0 ‚Üí n ‚Ä¢ (div a n) = a)

lemma smul_surj_of_divisible_by [divisible_by A Œ±] {n : Œ±} (hn : n ‚â† 0) :
  function.surjective ((‚Ä¢) n : A ‚Üí A) :=
Œª x, ‚ü®divisible_by.div x n, divisible_by.div_cancel _ hn‚ü©

/--
An `add_monoid A` is `Œ±`-divisible iff `n ‚Ä¢ _` is a surjective function, i.e. the constructive
version implies the textbook approach.
-/
noncomputable def divisible_by_of_smul_surj
  [Œ† (n : Œ±), decidable (n = 0)]
  (H : ‚àÄ {n : Œ±}, n ‚â† 0 ‚Üí function.surjective ((‚Ä¢) n : A ‚Üí A)) :
  divisible_by A Œ± :=
{ div := Œª a n, dite (n = 0) (Œª _, 0) (Œª hn, (H hn a).some),
  div_zero := Œª _, dif_pos rfl,
  div_cancel := Œª n a hn, by rw [dif_neg hn, (H hn a).some_spec] }

end add_monoid

namespace monoid

variables (A Œ± : Type*) [monoid A] [has_pow A Œ±] [has_zero Œ±]

/--
A `monoid A` is `Œ±`-rootable iff `x‚Åø = a` has a solution for all `n ‚â† 0 ‚àà Œ±` and `a ‚àà A`.
Here we adpot a constructive approach where we ask an explicit `root : A ‚Üí Œ± ‚Üí A` function such that
* `root a 0 = 1` for all `a ‚àà A`
* `(root a n)‚Åø = a` for all `n ‚â† 0 ‚àà Œ±` and `a ‚àà A`.
-/
@[to_additive add_monoid.divisible_by]
class rootable_by :=
(root : A ‚Üí Œ± ‚Üí A)
(root_zero : ‚àÄ a, root a 0 = 1)
(root_pow : ‚àÄ {n : Œ±} (a : A), n ‚â† 0 ‚Üí (root a n)^n = a)

@[to_additive add_monoid.smul_surj_of_divisible_by]
lemma root_surj_of_rootable_by [rootable_by A Œ±] {n : Œ±} (hn : n ‚â† 0) :
  function.surjective ((flip (^)) n : A ‚Üí A) :=
Œª x, ‚ü®rootable_by.root x n, rootable_by.root_pow _ hn‚ü©

/--
A `monoid A` is `Œ±`-rootable iff the `pow _ n` function is surjective, i.e. the constructivec version
implies the textbook approach.
-/
@[to_additive add_monoid.divisible_by_of_smul_surj]
noncomputable def rootable_by_of_root_surj
  [Œ† (n : Œ±), decidable (n = 0)]
  (H : ‚àÄ {n : Œ±}, n ‚â† 0 ‚Üí function.surjective ((flip (^)) n : A ‚Üí A)) :
rootable_by A Œ± :=
{ root := Œª a n, dite (n = 0) (Œª _, 1) (Œª hn, (H hn a).some),
  root_zero := Œª _, dif_pos rfl,
  root_pow := Œª n a hn, by rw dif_neg hn; exact (H hn a).some_spec }

end monoid

namespace add_group

open add_monoid

variables (A : Type*) [add_group A]

/--
An add_group is `‚Ñ§` divisible if it is `‚Ñï`-divisible.
-/
def divisible_by_int_of_divisible_by_nat [divisible_by A ‚Ñï] :
  divisible_by A ‚Ñ§ :=
{ div := Œª a z, match z with
  | int.of_nat m := (divisible_by.div a m)
  | int.neg_succ_of_nat m := - (divisible_by.div a (m + 1))
  end,
  div_zero := Œª a, divisible_by.div_zero a,
  div_cancel := Œª z a hn, begin
    cases z,
    { norm_num,
      change z ‚Ä¢ (divisible_by.div _ _) = _,
      rw divisible_by.div_cancel _ _,
      rw [int.of_nat_eq_coe] at hn,
      exact_mod_cast hn, },
    { norm_num,
      change - ((z+1) ‚Ä¢ - (divisible_by.div _ _)) = _,
      have := nsmul_zero_sub (divisible_by.div a (z + 1)) (z + 1),
      rw [zero_sub, zero_sub] at this,
      rw [this, neg_neg, divisible_by.div_cancel],
      norm_num, },
  end}

/--
An add_group is `‚Ñï`-divisible if it is `‚Ñ§`-divisible.
-/
def divisible_by_nat_of_divisible_by_int [divisible_by A ‚Ñ§] :
  divisible_by A ‚Ñï :=
{ div := Œª a n, divisible_by.div a (n : ‚Ñ§),
  div_zero := Œª a, divisible_by.div_zero a,
  div_cancel := Œª n a hn, begin
    have := divisible_by.div_cancel a (by exact_mod_cast hn : (n : ‚Ñ§) ‚â† 0),
    norm_num at this,
    assumption,
  end, }

end add_group
#lint
#exit
namespace add_comm_group

open_locale pointwise

variables (A : Type*) [add_comm_group A]

/--
A divisible group `A` is an abelian group such that `nA = A` for all `n ‚â† 0`.
-/
class divisible :=
(div_int : A ‚Üí ‚Ñ§ ‚Üí A)
(div_zero : ‚àÄ a, div_int a 0 = 0)
(div_cancel : ‚àÄ {n : ‚Ñ§} (a : A), n ‚â† 0 ‚Üí n ‚Ä¢ div_int a n = a)

localized "infix ` /‚Ñ§ ` :50 := divisible.div_int" in divisible_group

lemma smul_surj_of_divisible [divisible A] {n : ‚Ñ§} (hn : n ‚â† 0) :
  function.surjective ((‚Ä¢) n : A ‚Üí A) :=
Œª a, ‚ü®a /‚Ñ§ n, divisible.div_cancel a hn‚ü©

noncomputable instance divisible_of_smul_surj
  (surj : ‚àÄ {n : ‚Ñ§}, n ‚â† 0 ‚Üí function.surjective ((‚Ä¢) n : A ‚Üí A)) :
  divisible A :=
{ div_int := Œª a n, dite (n = 0) (Œª _, 0) (Œª h, (surj h a).some),
  div_zero := Œª a, dif_pos rfl,
  div_cancel := Œª n a h, begin
    rw [dif_neg h, (surj h a).some_spec],
  end }

lemma smul_top_eq_top_of_divisible [divisible A] {n : ‚Ñ§} (hn : n ‚â† 0) :
  n ‚Ä¢ (‚ä§ : add_subgroup A) = ‚ä§ :=
add_subgroup.map_top_of_surjective _ $ smul_surj_of_divisible _ hn

noncomputable instance divisible_of_smul_top_eq_top
  (H : ‚àÄ {n : ‚Ñ§}, n ‚â† 0 ‚Üí n ‚Ä¢ (‚ä§ : add_subgroup A) = ‚ä§) :
  divisible A:=
add_comm_group.divisible_of_smul_surj _ $ Œª n hn a, ‚ü®(show a ‚àà n ‚Ä¢ (‚ä§ : add_subgroup A),
  by simp only [H hn]).some, (show a ‚àà n ‚Ä¢ (‚ä§ : add_subgroup A), by simp only [H hn]).some_spec.2‚ü©

/-- Any division ring of characteristic zero is divisible -/
@[priority 100]
instance divisible_of_char_zero {ùïú} [division_ring ùïú] [char_zero ùïú] : divisible ùïú :=
{ div_int := Œª q n, q / n,
  div_zero := Œª q, by norm_num,
  div_cancel := Œª n q hn,
    by rw [zsmul_eq_mul, (int.cast_commute n _).eq, div_mul_cancel q (int.cast_ne_zero.mpr hn)] }

section pi

variables {Œπ : Type*} (B : Œπ ‚Üí Type*) [Œ† i, add_comm_group (B i)] [Œ† i, divisible (B i)]

/-- Any product of divisible group is divisible.-/
instance divisible_pi : divisible (Œ† i, B i) :=
{ div_int := Œª x n i, (x i) /‚Ñ§ n,
  div_zero := Œª x, funext $ Œª i, divisible.div_zero _,
  div_cancel := Œª n x hn, funext $ Œª i, divisible.div_cancel (x i) hn }

end pi

section prod

variable [divisible A]
variables (B : Type*) [add_comm_group B] [divisible B]

instance divisible_prod : divisible (A √ó B) :=
{ div_int := Œª p n, ‚ü®p.1 /‚Ñ§ n, p.2 /‚Ñ§ n‚ü©,
  div_zero := Œª p, prod.ext (divisible.div_zero _) (divisible.div_zero _),
  div_cancel := Œª n p hn, (prod.ext (divisible.div_cancel p.1 hn) (divisible.div_cancel p.2 hn)) }

end prod


section quotient

variables {B : add_subgroup A} [divisible A]

noncomputable instance divisible_quotient : divisible (A ‚ß∏ B) :=
add_comm_group.divisible_of_smul_surj _ $ Œª n hn x, quotient.induction_on' x $ Œª a,
  ‚ü®quotient.mk' (a /‚Ñ§ n), (congr_arg _ $ divisible.div_cancel a hn : quotient.mk' _ = _)‚ü©

end quotient

section hom

variables {A} [divisible A] {B : Type*} [add_comm_group B] (f : A ‚Üí+ B)

noncomputable instance divisible_of_surj (hf : function.surjective f) : divisible B :=
add_comm_group.divisible_of_smul_surj _ $
  Œª n hn x, ‚ü®f $ (hf x).some /‚Ñ§ n, by rw [‚Üêf.map_zsmul ((hf x).some /‚Ñ§ n) n,
    divisible.div_cancel _ hn, (hf x).some_spec]‚ü©

end hom

end add_comm_group
