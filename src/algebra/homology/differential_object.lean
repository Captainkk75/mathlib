/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.homology.homological_complex
import category_theory.differential_object

/-!
# Homological complexes are differential graded objects.

We verify that a `homological_complex` indexed by an `add_comm_group` is
essentially the same thing as a differential graded object.

This equivalence is probably not particularly useful in practice;
it's here to check that definitions match up as expected.
-/

open category_theory
open category_theory.limits

open_locale classical
noncomputable theory

namespace homological_complex

variables {Œ≤ Œ≥ : Type*} [add_right_cancel_monoid Œ≤] [has_one Œ≥] [add_comm_monoid Œ≥] {f : Œ≥ ‚Üí+ Œ≤}
variables {V : Type*} [category V] [has_zero_morphisms V]

/-- Since `eq_to_hom` only preserves the fact that `X.X i = X.X j` but not `i = j`, this definition
is used to aid the simplifier. -/
abbreviation _root_.category_theory.differential_object.X_eq_to_hom
  (X : differential_object (graded_object_with_shift f V) Œ≥)
  {i j : Œ≤} (h : i = j) : X.X i ‚ü∂ X.X j := eq_to_hom (congr_arg X.X h)

@[simp] lemma _root_.category_theory.differential_object.X_eq_to_hom_refl
  (X : differential_object (graded_object_with_shift f V) Œ≥) (i : Œ≤) :
    X.X_eq_to_hom (refl i) = ùüô _ := rfl

@[simp, reassoc] lemma eq_to_hom_d (X : differential_object (graded_object_with_shift f V) Œ≥)
  {x y : Œ≤} (h : x = y) :
  X.X_eq_to_hom h ‚â´ X.d y = X.d x ‚â´ X.X_eq_to_hom (by simp [h]) :=
by { cases h, erw [category.comp_id, category.id_comp] }

@[simp, reassoc] lemma d_eq_to_hom (X : homological_complex V (complex_shape.up' (f 1)))
  {x y z : Œ≤} (h : y = z) :
  X.d x y ‚â´ eq_to_hom (congr_arg X.X h) = X.d x z :=
by { cases h, simp }

@[simp, reassoc] lemma eq_to_hom_f {X Y : differential_object (graded_object_with_shift f V) Œ≥}
  (f : X ‚ü∂ Y) {x y : Œ≤} (h : x = y) :
  X.X_eq_to_hom h ‚â´ f.f y = f.f x ‚â´ Y.X_eq_to_hom h :=
by { cases h, simp }

variables (f V)

local attribute [reducible] graded_object.has_shift_of_add_monoid_hom

/--
The functor from differential graded objects to homological complexes.
-/
@[simps]
def dgo_to_homological_complex :
  differential_object (graded_object_with_shift f V) Œ≥ ‚•§
    homological_complex V (complex_shape.up' (f 1)) :=
{ obj := Œª X,
  { X := Œª i, X.X i,
    d := Œª i j, if h : i + f 1 = j then X.d i ‚â´ X.X_eq_to_hom h else 0,
    shape' := Œª i j w, by { dsimp at w, convert dif_neg w },
    d_comp_d' := Œª i j k hij hjk, begin
      dsimp at hij hjk, substs hij hjk,
      have : X.d i ‚â´ X.d _ = _ := (congr_fun X.d_squared i : _),
      dsimp,
      simp only [if_true, eq_self_iff_true],
      erw [category.comp_id, category.comp_id, this],
      refl
    end },
  map := Œª X Y g,
  { f := g.f,
    comm' := Œª i j h, begin
      dsimp at h ‚ä¢,
      subst h,
      have : g.f i ‚â´ Y.d i = X.d i ‚â´ g.f (i + f 1) := (congr_fun g.comm i).symm,
      reassoc! this,
      simp only [category.comp_id, eq_to_hom_refl, dif_pos rfl, this, category.assoc, eq_to_hom_f]
    end, } }

/--
The functor from homological complexes to differential graded objects.
-/
@[simps]
def homological_complex_to_dgo :
  homological_complex V (complex_shape.up' (f 1)) ‚•§
    differential_object (graded_object_with_shift f V) Œ≥ :=
{ obj := Œª X,
  { X := Œª i, X.X i,
    d := Œª i, X.d i (i + f 1),
    d_squared' := by { ext i, dsimp, simp } },
  map := Œª X Y f,
  { f := f.f,
    comm' := by { ext i, dsimp, simp, }, } }

/--
The unit map for `dgo_equiv_homological_complex`.
-/
@[simps]
def dgo_equiv_homological_complex_unit_iso_apply
  (X : differential_object (graded_object_with_shift f V) Œ≥) :
  X ‚ü∂ (homological_complex_to_dgo f V).obj ((dgo_to_homological_complex f V).obj X) :=
{ f := Œª i, ùüô (X.X i) }

/--
The inverse unit map for `dgo_equiv_homological_complex`.
-/
@[simps]
def dgo_equiv_homological_complex_unit_iso_inv_apply
  (X : differential_object (graded_object_with_shift f V) Œ≥) :
  (homological_complex_to_dgo f V).obj ((dgo_to_homological_complex f V).obj X) ‚ü∂ X :=
{ f := Œª i, ùüô (X.X i) }

/--
The unit isomorphism for `dgo_equiv_homological_complex`.
-/
@[simps]
def dgo_equiv_homological_complex_unit_iso :
  ùü≠ (differential_object (graded_object_with_shift f V) Œ≥) ‚âÖ
    dgo_to_homological_complex f V ‚ãô homological_complex_to_dgo f V :=
nat_iso.of_components (Œª X,
  { hom := dgo_equiv_homological_complex_unit_iso_apply f V X,
    inv := dgo_equiv_homological_complex_unit_iso_inv_apply f V X }) (by tidy)

/--
The counit isomorphism for `dgo_equiv_homological_complex`.
-/
@[simps]
def dgo_equiv_homological_complex_counit_iso :
  homological_complex_to_dgo f V ‚ãô dgo_to_homological_complex f V ‚âÖ
    ùü≠ (homological_complex V (complex_shape.up' (f 1))) :=
nat_iso.of_components (Œª X,
  { hom :=
    { f := Œª i, ùüô (X.X i),
      comm' := Œª i j h, begin
        dsimp at h ‚ä¢, subst h,
        delta homological_complex_to_dgo,
        simp,
      end },
    inv :=
    { f := Œª i, ùüô (X.X i),
      comm' := Œª i j h, begin
        dsimp at h ‚ä¢, subst h,
        delta homological_complex_to_dgo,
        simp,
      end }, }) (by tidy)

/--
The category of differential graded objects in `V` is equivalent
to the category of homological complexes in `V`.
-/
@[simps]
def dgo_equiv_homological_complex :
  differential_object (graded_object_with_shift f V) Œ≥ ‚âå
    homological_complex V (complex_shape.up' (f 1)) :=
{ functor := dgo_to_homological_complex f V,
  inverse := homological_complex_to_dgo f V,
  unit_iso := dgo_equiv_homological_complex_unit_iso f V,
  counit_iso := dgo_equiv_homological_complex_counit_iso f V, }

local attribute [instance] endofunctor_monoidal_category

instance : has_shift (homological_complex V (complex_shape.up' (f 1))) Œ≥ :=
‚ü®(shift_monoidal_functor _ _).comp (comp_equiv_monoidal (dgo_equiv_homological_complex f V))‚ü©

end homological_complex
