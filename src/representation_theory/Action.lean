/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.category.Group.basic
import category_theory.single_obj
import category_theory.equivalence

/-!
# `Action V G`, the category of actions of a monoid `G` inside some category `V`.

The prototypical example is `V = Module R`,
where `Action (Module R) G` is the category of `R`-linear representations of `G`.

We check `Action V G ‚âå (single_obj G ‚•§ V)`,
and construct the restriction functors `res {G H : Mon} (f : G ‚ü∂ H) : Action V H ‚•§ Action V G`.
-/

universes u

open category_theory

variables (V : Type (u+1)) [large_category V]

/--
An `Action V G` represents a bundled action of
the monoid `G` on an object of some category `V`.

As an example, when `V = Module R`, this is an `R`-linear representation of `G`,
while when `V = Type` this is a `G`-action.
-/
-- Note: this is _not_ a categorical action of `G` on `V`.
structure Action (G : Mon.{u}) :=
(V : V)
(œÅ : G ‚ü∂ Mon.of (End V))

namespace Action
variable {V}

@[simp]
lemma œÅ_one {G : Mon.{u}} (A : Action V G) : A.œÅ 1 = ùüô A.V :=
by { rw [monoid_hom.map_one], refl, }

/-- When a group acts, we can lift the action to the group of automorphisms. -/
@[simps]
def œÅ_Aut {G : Group.{u}} (A : Action V (Mon.of G)) : G ‚ü∂ Group.of (Aut A.V) :=
{ to_fun := Œª g,
  { hom := A.œÅ g,
    inv := A.œÅ (g‚Åª¬π : G),
    hom_inv_id' := ((A.œÅ).map_mul (g‚Åª¬π : G) g).symm.trans (by rw [inv_mul_self, œÅ_one]),
    inv_hom_id' := ((A.œÅ).map_mul g (g‚Åª¬π : G)).symm.trans (by rw [mul_inv_self, œÅ_one]), },
  map_one' := by { ext, exact A.œÅ.map_one },
  map_mul' := Œª x y, by { ext, exact A.œÅ.map_mul x y }, }

variable (G : Mon.{u})

section

/-- The trivial representation of a group. -/
def trivial : Action AddCommGroup G :=
{ V := AddCommGroup.of punit,
  œÅ := 1, }

instance : inhabited (Action AddCommGroup G) := ‚ü®trivial G‚ü©
end

variables {G V}

/--
A homomorphism of `Action V G`s is a morphism between the underlying objects,
commuting with the action of `G`.
-/
@[ext]
structure hom (M N : Action V G) :=
(hom : M.V ‚ü∂ N.V)
(comm' : ‚àÄ g : G, M.œÅ g ‚â´ hom = hom ‚â´ N.œÅ g . obviously)

restate_axiom hom.comm'

namespace hom

/-- The identity morphism on a `Action V G`. -/
@[simps]
def id (M : Action V G) : Action.hom M M :=
{ hom := ùüô M.V }

instance (M : Action V G) : inhabited (Action.hom M M) := ‚ü®id M‚ü©

/--
The composition of two `Action V G` homomorphisms is the composition of the underlying maps.
-/
@[simps]
def comp {M N K : Action V G} (p : Action.hom M N) (q : Action.hom N K) :
  Action.hom M K :=
{ hom := p.hom ‚â´ q.hom,
  comm' := Œª g, by rw [‚Üêcategory.assoc, p.comm, category.assoc, q.comm, ‚Üêcategory.assoc] }

end hom

instance : category (Action V G) :=
{ hom := Œª M N, hom M N,
  id := Œª M, hom.id M,
  comp := Œª M N K f g, hom.comp f g, }

@[simp]
lemma id_hom (M : Action V G) : (ùüô M : hom M M).hom = ùüô M.V := rfl
@[simp]
lemma comp_hom {M N K : Action V G} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
  (f ‚â´ g : hom M K).hom = f.hom ‚â´ g.hom :=
rfl

/-- Construct an isomorphism of `G` actions/representations
from an isomorphism of the the underlying objects,
where the forward direction commutes with the group action. -/
@[simps]
def mk_iso {M N : Action V G} (f : M.V ‚âÖ N.V) (comm : ‚àÄ g : G, M.œÅ g ‚â´ f.hom = f.hom ‚â´ N.œÅ g) :
  M ‚âÖ N :=
{ hom :=
  { hom := f.hom,
    comm' := comm, },
  inv :=
  { hom := f.inv,
    comm' := Œª g, by { have w := comm g =‚â´ f.inv, simp at w, simp [w], }, }}

namespace functor_category_equivalence

/-- Auxilliary definition for `functor_category_equivalence`. -/
@[simps]
def functor : Action V G ‚•§ (single_obj G ‚•§ V) :=
{ obj := Œª M,
  { obj := Œª _, M.V,
    map := Œª _ _ g, M.œÅ g,
    map_id' := Œª _, M.œÅ.map_one,
    map_comp' := Œª _ _ _ g h, M.œÅ.map_mul h g, },
  map := Œª M N f,
  { app := Œª _, f.hom,
    naturality' := Œª _ _ g, f.comm g, } }

/-- Auxilliary definition for `functor_category_equivalence`. -/
@[simps]
def inverse : (single_obj G ‚•§ V) ‚•§ Action V G :=
{ obj := Œª F,
  { V := F.obj punit.star,
    œÅ :=
    { to_fun := Œª g, F.map g,
      map_one' := F.map_id punit.star,
      map_mul' := Œª g h, F.map_comp h g, } },
  map := Œª M N f,
  { hom := f.app punit.star,
    comm' := Œª g, f.naturality g, } }.

/-- Auxilliary definition for `functor_category_equivalence`. -/
def unit_iso : ùü≠ (Action V G) ‚âÖ functor ‚ãô inverse :=
nat_iso.of_components (Œª M, mk_iso ((iso.refl _)) (by tidy)) (by tidy).

/-- Auxilliary definition for `functor_category_equivalence`. -/
def counit_iso : inverse ‚ãô functor ‚âÖ ùü≠ (single_obj G ‚•§ V) :=
nat_iso.of_components (Œª M, nat_iso.of_components (by tidy) (by tidy)) (by tidy).

end functor_category_equivalence

section
open functor_category_equivalence

/--
The category of actions of `G` in the category `V`
is equivalent to the functor category `single_obj G ‚•§ V`.
-/
def functor_category_equivalence : Action V G ‚âå (single_obj G ‚•§ V) :=
{ functor := functor,
  inverse := inverse,
  unit_iso := unit_iso,
  counit_iso := counit_iso, }

end

section forget

/-- (implementation) The forgetful functor from bundled actions to the underlying objects.

Use the `category_theory.forget` API provided by the `concrete_category` instance below,
rather than using this directly.
-/
def forget : Action V G ‚•§ V :=
{ obj := Œª M, M.V,
  map := Œª M N f, f.hom, }

instance [concrete_category V] : concrete_category (Action V G) :=
{ forget := forget ‚ãô (concrete_category.forget V),
  forget_faithful :=
  { map_injective' := Œª M N f g w,
      hom.ext _ _ (faithful.map_injective (concrete_category.forget V) w), } }

instance has_forget_to_V [concrete_category V] : has_forget‚ÇÇ (Action V G) V :=
{ forget‚ÇÇ := forget }

end forget

/-- Actions/representations of the trivial group are just objects in the ambient category. -/
def Action_punit_equivalence : Action V (Mon.of punit) ‚âå V :=
{ functor := forget,
  inverse :=
  { obj := Œª X, ‚ü®X, 1‚ü©,
    map := Œª X Y f, ‚ü®f, Œª ‚ü®‚ü©, by simp‚ü©, },
  unit_iso := nat_iso.of_components (Œª X, mk_iso (iso.refl _) (Œª ‚ü®‚ü©, by simpa using œÅ_one X))
    (by tidy),
  counit_iso := nat_iso.of_components (Œª X, iso.refl _) (by tidy), }

variables (V)
/--
The "restriction" functor along a monoid homomorphism `f : G ‚ü∂ H`,
taking actions of `H` to actions of `G`.

(This makes sense for any homomorphism, but the name is natural when `f` is a monomorphism.)
-/
@[simps]
def res {G H : Mon} (f : G ‚ü∂ H) : Action V H ‚•§ Action V G :=
{ obj := Œª M,
  { V := M.V,
    œÅ := f ‚â´ M.œÅ },
  map := Œª M N p,
  { hom := p.hom,
    comm' := Œª g, p.comm (f g) } }

/--
The natural isomorphism from restriction along the identity homomorphism to
the identity functor on `Action V G`.
-/
@[simps]
def res_id {G : Mon} : res V (ùüô G) ‚âÖ ùü≠ (Action V G) :=
{ hom := { app := Œª M, ‚ü®ùüô M.V‚ü© },
  inv := { app := Œª M, ‚ü®ùüô M.V‚ü© }, }

/--
The natural isomorphism from the composition of restrictions along homomorphisms
to the restriction along the composition of homomorphism.
-/
@[simps]
def res_comp {G H K : Mon} (f : G ‚ü∂ H) (g : H ‚ü∂ K) : res V g ‚ãô res V f ‚âÖ res V (f ‚â´ g) :=
{ hom := { app := Œª M, ‚ü®ùüô M.V‚ü© },
  inv := { app := Œª M, ‚ü®ùüô M.V‚ü© },
  hom_inv_id' := by { ext, dsimp, simp, },
  inv_hom_id' := by { ext, dsimp, simp, }, }

-- TODO promote `res` to a pseudofunctor from
-- the locally discrete bicategory constructed from `Mon·µí·µñ` to `Cat`, sending `G` to `Action V G`.

-- TODO limits, colimits, images, etc, when `V` has them
-- TODO (symmetric) monoidal category structure when `V` has one

end Action

namespace category_theory.functor

variables {V} {W : Type (u+1)} [large_category W]

/-- A functor between categories induces a functor between
the categories of `G`-actions within those categories. -/
@[simps]
def map_Action (F : V ‚•§ W) (G : Mon.{u‚ÇÅ}) : Action V G ‚•§ Action W G :=
{ obj := Œª M,
  { V := F.obj M.V,
    œÅ :=
    { to_fun := Œª g, F.map (M.œÅ g),
      map_one' := by tidy,
      map_mul' := by simp, }},
  map := Œª M N f,
  { hom := F.map f.hom,
    comm' := Œª g, by { dsimp, rw [‚ÜêF.map_comp, f.comm, F.map_comp] } }, }

end category_theory.functor
