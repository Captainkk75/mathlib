/-
Copyright (c) 2022 Hanting Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Dilations of emetric and metric spaces
Authors: Hanting Zhang
-/
import topology.metric_space.antilipschitz
import data.fun_like.basic

/-!
# Dilations

We define dilations, i.e., maps between emetric spaces that
satisfy `edist (f x) (f y) = r * edist x y`.

Defines `ratio f`, which is the ratio of the dilation `f`

Here `r : ‚Ñù‚â•0`, so we do not exclude the degenerate case of dilations
which collapse into constant maps. Since there is no `‚Ñù>0` API defined in mathlib,
no matter where you choose to exclude r = 0, you will end up having to carry
it around with you anyways. So statements that do need strict dilations should
just say `f : dilation Œ± Œ≤ r` and `hr : r ‚â† 0`.

TODO: Introduce dilation equivs. Refactor the isometry API
to match the `*_hom_class` API below.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `pseudo_metric_space` and we specialize to `metric_space` when needed.
-/

noncomputable theory

open function set
open_locale topological_space ennreal nnreal

section setup

variables (Œ± : Type*) (Œ≤ : Type*) [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤]

/-- A dilation is a map that uniformly scales the edistance between any two points.  -/
structure dilation :=
(to_fun : Œ± ‚Üí Œ≤)
(edist_eq' : ‚àÉ r : ‚Ñù‚â•0, ‚àÄ x y : Œ±, edist (to_fun x) (to_fun y) = r * edist x y)

attribute [nolint has_inhabited_instance] dilation

/--
`dilation_class F Œ± Œ≤ r` states that `F` is a type of `r`-dilations.

You should extend this typeclass when you extend `dilation`.
-/
class dilation_class (F : Type*) (Œ± Œ≤ : out_param $ Type*)
  [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] extends fun_like F Œ± (Œª _, Œ≤) :=
(edist_eq' : ‚àÄ (f : F), ‚àÉ r : ‚Ñù‚â•0, ‚àÄ (x y : Œ±), edist (f x) (f y) = r * edist x y)

attribute [nolint dangerous_instance] dilation_class.to_fun_like

instance dilation.to_dilation_class :
  dilation_class (dilation Œ± Œ≤) Œ± Œ≤ :=
{ coe := dilation.to_fun,
  coe_injective' := Œª f g h, by { cases f; cases g; congr', },
  edist_eq' := Œª f, dilation.edist_eq' f }

instance : has_coe_to_fun (dilation Œ± Œ≤) (Œª _, Œ± ‚Üí Œ≤) := fun_like.has_coe_to_fun

@[simp] lemma dilation.to_fun_eq_coe {f : dilation Œ± Œ≤} : f.to_fun = (f : Œ± ‚Üí Œ≤) := rfl

@[ext] theorem dilation.ext {f g : dilation Œ± Œ≤} (h : ‚àÄ x, f x = g x) : f = g :=
fun_like.ext f g h

/-- Copy of a `dilation` with a new `to_fun` equal to the old one. Useful to fix definitional
equalities. -/
protected def dilation.copy (f : dilation Œ± Œ≤) (f' : Œ± ‚Üí Œ≤) (h : f' = ‚áëf) : dilation Œ± Œ≤ :=
{ to_fun := f',
  edist_eq' := h.symm ‚ñ∏ f.edist_eq' }

/-- The ratio of a dilation `f`. Uses `Exists.some` -/
def ratio {Œ± Œ≤} [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] {F : Type*}
  [dilation_class F Œ± Œ≤] (f : F) : ‚Ñù‚â•0 :=
(dilation_class.edist_eq' f).some

end setup

namespace dilation

variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {F : Type*} {G : Type*}

@[simp] lemma edist_eq
  [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) (x y : Œ±) : edist (f x) (f y) = ratio f * edist x y :=
(dilation_class.edist_eq' f).some_spec x y

@[simp] lemma dist_eq
  [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) (x y : Œ±) : dist (f x) (f y) = ratio f * dist x y :=
by simp only [dist_edist, edist_eq, ennreal.to_real_mul, ennreal.coe_to_real]

theorem nndist_eq [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) (x y : Œ±) : nndist (f x) (f y) = ratio f * nndist x y :=
begin
  apply subtype.ext _,
  simp only [coe_nndist, dist_eq, nonneg.coe_mul],
end

section pseudo_emetric_dilation

variables [pseudo_emetric_space Œ±] [pseudo_emetric_space Œ≤] [pseudo_emetric_space Œ≥]
variables [dilation_class F Œ± Œ≤] [dilation_class G Œ≤ Œ≥]
variables (f : F) (g : G) {x y z : Œ±}  {s : set Œ±}

lemma lipschitz : lipschitz_with (ratio f) (f : Œ± ‚Üí Œ≤) :=
Œª x y, (edist_eq f x y).le


-- TODO: add `instance ennreal.div_inv_comm_monoid`
-- TODO: fix `antilipschitz_with` decl header
lemma antilipschitz (hr : ratio f ‚â† 0) : antilipschitz_with ((ratio f)‚Åª¬π) (f : Œ± ‚Üí Œ≤) :=
Œª x y, begin
  -- `div_eq_mul_inv` should be `div_eq_inv_mul`
  rw [mul_comm, ‚Üê ennreal.div_le_iff_le_mul, div_eq_mul_inv, mul_comm, ennreal.coe_inv hr],
  simp only [inv_inv, edist_eq, le_refl],
  left, simp [hr],
  left, simp [hr],
end

/-- A dilation from an emetric space is injective -/
lemma injective {Œ± : Type*} [emetric_space Œ±]  [dilation_class F Œ± Œ≤] (f : F) (hr : ratio f ‚â† 0) :
  injective f := (dilation.antilipschitz f hr).injective

/-- Any map on a subsingleton is a dilation -/
def of_subsingleton [subsingleton Œ±] (f : Œ± ‚Üí Œ≤) : dilation Œ± Œ≤ :=
{ to_fun := f,
  edist_eq' := ‚ü®0, Œª x y, by { rw subsingleton.elim x y, simp, }‚ü© }

/-- The composition of similarities is a dilation -/
def comp (f : dilation Œ± Œ≤) (g : dilation Œ≤ Œ≥):
  dilation Œ± Œ≥ :=
{ to_fun := g ‚àò f,
  edist_eq' := ‚ü®ratio f * ratio g, Œª x y, by { simp only [edist_eq, ennreal.coe_mul], ring, }‚ü© }

/-- The constant function of is a dilation -/
def const (b : Œ≤) :
  dilation Œ± Œ≤ :=
{ to_fun := Œª _, b,
  edist_eq' := ‚ü®0, Œª x y, by simp‚ü© }

/-- A dilation from a metric space is a uniform inducing map -/
theorem uniform_inducing (hr : ratio f ‚â† 0) :
  uniform_inducing (f : Œ± ‚Üí Œ≤) :=
(antilipschitz f hr).uniform_inducing (lipschitz f).uniform_continuous

lemma tendsto_nhds_iff {Œπ : Type*} {g : Œπ ‚Üí Œ±} {a : filter Œπ} {b : Œ±} (hr : ratio f ‚â† 0) :
  filter.tendsto g a (ùìù b) ‚Üî filter.tendsto ((f : Œ± ‚Üí Œ≤) ‚àò g) a (ùìù (f b)) :=
(uniform_inducing f hr).inducing.tendsto_nhds_iff

/-- A dilation is continuous. -/
lemma to_continuous : continuous (f : Œ± ‚Üí Œ≤) :=
(lipschitz f).continuous

/-- Similarities multiply the diameter by their ratio in pseudoemetric spaces. -/
lemma ediam_image (s : set Œ±) :
  emetric.diam ((f: Œ± ‚Üí Œ≤) '' s) = ratio f * emetric.diam s :=
begin
  apply le_antisymm,
  { exact lipschitz_with.ediam_image_le (lipschitz f) s },
  by_cases hr : ratio f ‚â† 0,
  { rw [mul_comm, ‚Üê ennreal.le_div_iff_mul_le, div_eq_mul_inv, mul_comm, ‚Üê ennreal.coe_inv hr],
    refine antilipschitz_with.le_mul_ediam_image (antilipschitz f hr) s,
    left, simp [hr],
    left, simp [hr], },
  rw not_not at hr,
  simp [hr],
end

lemma ediam_range :
  emetric.diam (range (f : Œ± ‚Üí Œ≤)) = ratio f * emetric.diam (univ : set Œ±) :=
by { rw ‚Üê image_univ, exact ediam_image f univ }

lemma maps_to_emetric_ball  (hr : ratio f ‚â† 0) (x : Œ±) (r' : ‚Ñù‚â•0‚àû) :
  maps_to (f : Œ± ‚Üí Œ≤) (emetric.ball x r') (emetric.ball (f x) (ratio f * r')) :=
begin
  intros y hy,
  simp only [emetric.mem_ball, edist_eq] at *,
  rwa ennreal.mul_lt_mul_left _ _,
  simp [hr],
  simp [hr],
end

lemma maps_to_emetric_closed_ball (x : Œ±) (r' : ‚Ñù‚â•0‚àû) :
  maps_to (f : Œ± ‚Üí Œ≤) (emetric.closed_ball x r') (emetric.closed_ball (f x) (ratio f * r')) :=
begin
  by_cases hr : ratio f ‚â† 0,
  { intros y hy,
    simp only [emetric.mem_closed_ball, edist_eq] at *,
    rwa ennreal.mul_le_mul_left _ _,
    simp [hr],
    simp [hr], },
  rw not_not at hr,
  simp [hr, maps_to],
end

lemma comp_continuous_on_iff
  {Œ≥} [topological_space Œ≥] {g : Œ≥ ‚Üí Œ±} {s : set Œ≥} (hr : ratio f ‚â† 0) :
  continuous_on ((f : Œ± ‚Üí Œ≤) ‚àò g) s ‚Üî continuous_on g s :=
(uniform_inducing f hr).inducing.continuous_on_iff.symm

lemma comp_continuous_iff
  {Œ≥} [topological_space Œ≥] {g : Œ≥ ‚Üí Œ±} (hr : ratio f ‚â† 0) :
  continuous ((f : Œ± ‚Üí Œ≤) ‚àò g) ‚Üî continuous g :=
(uniform_inducing f hr).inducing.continuous_iff.symm

end pseudo_emetric_dilation --section

section emetric_dilation
variables [emetric_space Œ±]

/-- A dilation from a metric space is a uniform embedding -/
theorem uniform_embedding [pseudo_emetric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) (hr : ratio f ‚â† 0) : uniform_embedding f :=
(antilipschitz f hr).uniform_embedding (lipschitz f).uniform_continuous

/-- A dilation from a metric space is an embedding -/
theorem embedding [pseudo_emetric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) (hr : ratio f ‚â† 0) : embedding (f : Œ± ‚Üí Œ≤) :=
(uniform_embedding f hr).embedding

/-- A dilation from a complete emetric space is a closed embedding -/
theorem closed_embedding
  [complete_space Œ±] [emetric_space Œ≤] [dilation_class F Œ± Œ≤]
  (f : F) (hr : ratio f ‚â† 0) : closed_embedding (f : Œ± ‚Üí Œ≤) :=
(antilipschitz f hr).closed_embedding (lipschitz f).uniform_continuous

end emetric_dilation --section

section pseudo_metric_dilation
variables [pseudo_metric_space Œ±] [pseudo_metric_space Œ≤] [dilation_class F Œ± Œ≤] (f : F)

/-- An isometry preserves the diameter in pseudometric spaces. -/
lemma diam_image (s : set Œ±) : metric.diam ((f : Œ± ‚Üí Œ≤) '' s) = ratio f * metric.diam s :=
by { simp [metric.diam, metric.diam, ediam_image, ennreal.to_real_mul], }

lemma diam_range : metric.diam (range (f : Œ± ‚Üí Œ≤)) = ratio f * metric.diam (univ : set Œ±) :=
by rw [‚Üê image_univ, diam_image]

lemma maps_to_ball (hr : ratio f ‚â† 0) (x : Œ±) (r' : ‚Ñù) :
  maps_to (f : Œ± ‚Üí Œ≤) (metric.ball x r') (metric.ball (f x) (ratio f * r')) :=
begin
  intros y hy,
  rw [metric.mem_ball, dist_eq],
  refine mul_lt_mul' (le_refl _) _ dist_nonneg _,
  rwa metric.mem_ball at hy,
  rwa [nnreal.coe_pos, pos_iff_ne_zero],
end

lemma maps_to_sphere (x : Œ±) (r' : ‚Ñù) :
  maps_to (f : Œ± ‚Üí Œ≤) (metric.sphere x r') (metric.sphere (f x) (ratio f * r')) :=
begin
  intros y hy,
  rw metric.mem_sphere at hy,
  rw [metric.mem_sphere, dist_eq, hy],
end

lemma maps_to_closed_ball (x : Œ±) (r' : ‚Ñù) :
  maps_to (f : Œ± ‚Üí Œ≤) (metric.closed_ball x r') (metric.closed_ball (f x) (ratio f * r')) :=
begin
  intros y hy,
  rw [metric.mem_closed_ball] at hy,
  rw [metric.mem_closed_ball, dist_eq],
  refine mul_le_mul (le_refl _) hy dist_nonneg nnreal.zero_le_coe,
end

end pseudo_metric_dilation -- section

end dilation
