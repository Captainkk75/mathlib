/-
Copyright (c) 2022 Mantas Bak≈°ys. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mantas Bak≈°ys
-/
import analysis.normed_space.pointwise
import measure_theory.measure.haar_lebesgue

/-!
# Cauchy's Functional Equation

This file contains the classical results about the Cauchy's functional equation
`f (x + y) = f x + f y` for functions `f : ‚Ñù ‚Üí ‚Ñù`. In this file, we prove that the solutions to this
equation are linear up to the case when `f` is a Lebesgue measurable functions, while also deducing
intermediate well-known variants.
-/

open add_monoid_hom measure_theory measure_theory.measure metric nnreal set
open_locale pointwise topological_space

variables {Œπ : Type*} [fintype Œπ] {s : set ‚Ñù} {a : ‚Ñù}

local notation `‚Ñù‚Åø` := Œπ ‚Üí ‚Ñù

/-- **Cauchy's functional equation**. An additive monoid homomorphism automatically preserves `‚Ñö`.
-/
theorem add_monoid_hom.is_linear_map_rat (f : ‚Ñù ‚Üí+ ‚Ñù) : is_linear_map ‚Ñö f :=
‚ü®map_add f, map_rat_cast_smul f ‚Ñù ‚Ñù‚ü©

-- should this one get generalised?
lemma exists_real_preimage_ball_pos_volume (f : ‚Ñù ‚Üí ‚Ñù) : ‚àÉ r z : ‚Ñù, 0 < volume (f‚Åª¬π' (ball z r)) :=
begin
  have : measure_space.volume (f ‚Åª¬π' univ) = ‚ä§ := real.volume_univ,
  by_contra' hf,
  simp only [nonpos_iff_eq_zero] at hf,
  have hrat : (‚ãÉ (q : ‚Ñö), ball (0 : ‚Ñù) q) = univ,
  { exact eq_univ_of_forall (Œª x, mem_Union.2 $ (exists_rat_gt _).imp $ Œª _, mem_ball_zero_iff.2)},
  simp only [‚Üêhrat, preimage_Union] at this,
  have htop : ‚ä§ ‚â§ ‚àë' (i : ‚Ñö), measure_space.volume ((Œª (q : ‚Ñö), f ‚Åª¬π' ball 0 ‚Üëq) i),
  { rw ‚Üêthis,
    exact measure_Union_le (Œª q : ‚Ñö, f‚Åª¬π' (ball (0 : ‚Ñù) q)) },
  simp only [hf, tsum_zero, nonpos_iff_eq_zero, ennreal.top_ne_zero] at htop,
  exact htop
end

lemma exists_zero_nhds_bounded (f : ‚Ñù ‚Üí+ ‚Ñù) (h : measurable f) :
  ‚àÉ s, s ‚àà ùìù (0 : ‚Ñù) ‚àß bounded (f '' s) :=
begin
  obtain ‚ü®r, z, hfr‚ü© := exists_real_preimage_ball_pos_volume f,
  refine ‚ü®_, sub_mem_nhds_zero_of_add_haar_pos volume (f‚Åª¬π' ball z r) (h $ measurable_set_ball) hfr,
    _‚ü©,
  rw image_sub,
  exact (bounded_ball.mono $ image_preimage_subset _ _).sub
    (bounded_ball.mono $ image_preimage_subset _ _),
end

lemma additive_continuous_at_zero_of_bounded_nhds_zero (f : ‚Ñù ‚Üí+ ‚Ñù) (hs : s ‚àà ùìù (0 : ‚Ñù))
  (hbounded : bounded (f '' s)) : continuous_at f 0 :=
begin
  rcases metric.mem_nhds_iff.mp hs with ‚ü®Œ¥, hŒ¥, hUŒµ‚ü©,
  obtain ‚ü®C, hC‚ü© := (bounded_iff_subset_ball _).1 (bounded.mono (image_subset f hUŒµ) hbounded),
  refine continuous_at_iff.2 (Œª Œµ hŒµ, _),
  simp only [gt_iff_lt, dist_zero_right, _root_.map_zero, exists_prop],
  cases exists_nat_gt (C / Œµ) with n hn,
  obtain hC0 | rfl | hC0 := lt_trichotomy C 0,
  { simp only [closed_ball_eq_empty.mpr hC0, image_subset_iff, preimage_empty] at hC,
    rw [subset_empty_iff, ball_eq_empty] at hC,
    linarith },
  { simp only [closed_ball_zero] at hC,
    refine ‚ü®Œ¥, hŒ¥, Œª x hxŒ¥, _‚ü©,
    rwa [mem_singleton_iff.1 (hC $ mem_image_of_mem f $ mem_ball_zero_iff.2 hxŒ¥), norm_zero] },
  have hnpos : 0 < (n : ‚Ñù) := (div_pos hC0 hŒµ).trans hn,
  refine ‚ü®Œ¥/n, div_pos hŒ¥ hnpos, Œª x hxŒ¥, _‚ü©,
  have h2 : f (n ‚Ä¢ x) = n ‚Ä¢ f x := map_nsmul f n x,
  simp_rw [nsmul_eq_mul, mul_comm (n : ‚Ñù), ‚Üêdiv_eq_iff hnpos.ne'] at h2,
  rw ‚Üêh2,
  replace hxŒ¥ : ‚à• x * n ‚à• < Œ¥,
  { simpa only [norm_mul, real.norm_coe_nat, ‚Üêlt_div_iff hnpos] using hxŒ¥ },
  norm_num,
  rw [div_lt_iff' hnpos, ‚Üêmem_ball_zero_iff],
  rw div_lt_iff hŒµ at hn,
  exact hC.trans (closed_ball_subset_ball hn) (mem_image_of_mem _ $ mem_ball_zero_iff.2 hxŒ¥),
end

lemma additive_continuous_at_zero (f : ‚Ñù ‚Üí+ ‚Ñù) (h : measurable f) : continuous_at f 0 :=
let ‚ü®s, hs, hbounded‚ü© := exists_zero_nhds_bounded f h in
  additive_continuous_at_zero_of_bounded_nhds_zero f hs hbounded

lemma measurable.continuous_real (f : ‚Ñù ‚Üí+ ‚Ñù) (h : measurable f) : continuous f :=
(f.uniform_continuous_of_continuous_at_zero $ additive_continuous_at_zero f h).continuous

-- do we want this one and where would it go?
lemma is_linear_map_iff_apply_eq_apply_one_mul {M : Type*} [comm_semiring M] (f : M ‚Üí+ M) :
  is_linear_map M f ‚Üî ‚àÄ x : M, f x = f 1 * x :=
begin
  refine ‚ü®Œª h x, _, Œª h, ‚ü®map_add f, Œª c x, _‚ü©‚ü©,
  { convert h.2 x 1 using 1,
    { simp only [algebra.id.smul_eq_mul, mul_one] },
    { simp only [mul_comm, algebra.id.smul_eq_mul] }},
  { rw [smul_eq_mul, smul_eq_mul, h (c * x), h x, ‚Üêmul_assoc, mul_comm _ c, mul_assoc] }
end

lemma is_linear_rat (f : ‚Ñù ‚Üí+ ‚Ñù) : ‚àÄ (q : ‚Ñö), f q = f 1 * q :=
begin
  intro q,
  have h1 : f ((q : ‚Ñù) ‚Ä¢ 1) = (q : ‚Ñù) ‚Ä¢ f 1 := map_rat_cast_smul f _ _ _ _,
  convert h1 using 1,
  { simp only [algebra.id.smul_eq_mul, mul_one], },
  { simp only [mul_comm, algebra.id.smul_eq_mul] }
end

lemma additive_is_bounded_of_bounded_on_interval (f : ‚Ñù ‚Üí+ ‚Ñù) (hs : s ‚àà ùìù a)
  (h : bounded (f '' s)) : ‚àÉ (V : set ‚Ñù), V ‚àà ùìù (0 : ‚Ñù) ‚àß bounded (f '' V) :=
begin
  rcases metric.mem_nhds_iff.mp hs with ‚ü®Œ¥, hŒ¥, hŒ¥a‚ü©,
  refine ‚ü®ball 0 Œ¥, ball_mem_nhds 0 hŒ¥, _‚ü©,
  rw bounded_iff_exists_norm_le,
  simp only [mem_image, mem_ball_zero_iff, forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ],
  rcases (bounded_iff_exists_norm_le.mp h) with ‚ü®M, hM‚ü©,
  simp only [mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ] at hM,
  refine ‚ü®M + M, Œª x hxŒ¥, (norm_le_add_norm_add _ $ f a).trans $ add_le_add _ $ hM _ $ hŒ¥a _‚ü©,
  { rw ‚Üêmap_add f,
    refine hM _ (hŒ¥a _),
    simp only [mem_ball],
    convert hxŒ¥,
    rw [‚Üêdist_zero_right, ‚Üêdist_add_right x 0 a, zero_add] },
  { simpa [mem_ball, dist_self] }
end

lemma continuous.is_linear_real (f : ‚Ñù ‚Üí+ ‚Ñù) (h : continuous f) : is_linear_map ‚Ñù f :=
begin
  rw is_linear_map_iff_apply_eq_apply_one_mul,
  have h1 := is_linear_rat f,
  refine Œª x, eq_of_norm_sub_le_zero (le_of_forall_pos_lt_add _),
  by_contra' hf,
  rcases hf with ‚ü®Œµ, hŒµ, hf‚ü©,
  rw continuous_iff at h,
  specialize h x (Œµ/2) (by linarith [hŒµ]),
  rcases h with ‚ü®Œ¥, hŒ¥, h‚ü©,
  by_cases hf1 : f 1 = 0,
  { simp only [hf1, zero_mul] at h1,
    simp only [hf1, zero_mul, sub_zero] at hf,
    cases (exists_rat_near x hŒ¥) with q hq,
    specialize h q _,
    { simp only [dist_eq_norm', real.norm_eq_abs, hq] },
    simp only [h1, dist_zero_left] at h,
    linarith },
  have hq : ‚àÉ (q : ‚Ñö), | x - ‚Üëq | < min Œ¥ (Œµ / 2 / ‚à•f 1‚à•),
  apply exists_rat_near,
  { refine lt_min hŒ¥ (mul_pos (by linarith) _),
    simp only [_root_.inv_pos, norm_pos_iff, ne.def, hf1, not_false_iff] },
  cases hq with q hq,
  specialize h ‚Üëq _,
  { simp only [dist_eq_norm', real.norm_eq_abs],
    exact hq.trans_le (min_le_left Œ¥ _) },
  rw [dist_eq_norm', h1] at h,
  suffices h2 : ‚à• f x - f 1 * x ‚à• < Œµ, by linarith [hf, h2],
  have h3 : ‚à• f x - f 1 * q ‚à• + ‚à• f 1 * q - f 1 * x ‚à• < Œµ,
  { have h4 : ‚à• f 1 * q - f 1 * x ‚à• < Œµ / 2,
    { replace hf1 : 0 < ‚à• f 1 ‚à• := by simpa [norm_pos_iff, ne.def],
      simp only [‚Üêmul_sub, norm_mul, mul_comm (‚à•f 1‚à•) _, ‚Üêlt_div_iff hf1],
      rw [‚Üêdist_eq_norm, dist_eq_norm', real.norm_eq_abs],
      apply lt_of_lt_of_le hq (min_le_right Œ¥ _) },
    linarith },
  refine ((norm_add_le _ _).trans_eq' _).trans_lt h3,
  congr,
  abel
end

-- to generalize
lemma add_monoid_hom.continuous_at_iff_continuous_at_zero (f : ‚Ñù ‚Üí+ ‚Ñù) :
  continuous_at f a ‚Üî continuous_at f 0 :=
begin
  refine ‚ü®Œª ha, continuous_at_iff.2 $ Œª Œµ hŒµ, Exists‚ÇÇ.imp (Œª Œ¥ hŒ¥, _) (continuous_at_iff.1 ha Œµ hŒµ),
    Œª h, (f.uniform_continuous_of_continuous_at_zero h).continuous.continuous_at‚ü©,
  refine Œª hŒ¥f y hyŒ¥, _,
  replace hyŒ¥ : dist (y + a) a < Œ¥,
  { convert hyŒ¥ using 1,
    simp only [dist_eq_norm, sub_zero, add_sub_cancel] },
  convert hŒ¥f hyŒ¥ using 1,
  simp only [dist_eq_norm, map_sub, _root_.map_add, _root_.map_zero, sub_zero, add_sub_cancel],
end

lemma continuous_at.is_linear_real (f : ‚Ñù ‚Üí+ ‚Ñù) (h : continuous_at f a) : is_linear_map ‚Ñù f :=
(f.uniform_continuous_of_continuous_at_zero $
  (f.continuous_at_iff_continuous_at_zero).mp h).continuous.is_linear_real f

lemma is_linear_map_real_of_bounded_nhds (f : ‚Ñù ‚Üí+ ‚Ñù) (hs : s ‚àà ùìù a) (hf : bounded (f '' s)) :
  is_linear_map ‚Ñù f :=
let ‚ü®V, hV0, hVb‚ü© := additive_is_bounded_of_bounded_on_interval f hs hf in
  (additive_continuous_at_zero_of_bounded_nhds_zero f hV0 hVb).is_linear_real f

lemma monotone_on.is_linear_map_real (f : ‚Ñù ‚Üí+ ‚Ñù) (hs : s ‚àà ùìù a) (hf : monotone_on f s) :
  is_linear_map ‚Ñù f :=
begin
  obtain ‚ü®t, ht, h‚ü© := metric.mem_nhds_iff.mp hs,
  refine is_linear_map_real_of_bounded_nhds f (closed_ball_mem_nhds a $ half_pos ht) _,
  replace h := (closed_ball_subset_ball $ half_lt_self ht).trans h,
  rw real.closed_ball_eq_Icc at ‚ä¢ h,
  have ha :  a - t / 2 ‚â§ a + t / 2 := by linarith,
  refine bounded_of_bdd_above_of_bdd_below (hf.map_bdd_above h _) (hf.map_bdd_below h _),
  { refine ‚ü®a + t / 2, _, h $ right_mem_Icc.2 ha‚ü©,
    rw upper_bounds_Icc ha,
    exact left_mem_Ici },
  { refine ‚ü®a - t / 2, _, h $ left_mem_Icc.2 ha‚ü©,
    rw lower_bounds_Icc ha,
    exact right_mem_Iic }
end
