/-
Copyright (c) 2022 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
import representation_theory.Action

/-!
# `Action V G` is a closed monoidal category when `G : Group` and `V` is a closed monoidal category.
-/

universe u

open opposite
open category_theory

variables
  {V : Type (u+1)} [large_category V] [monoidal_category V] [monoidal_closed V] {G : Group.{u}}
  (X Y Z : Action V G) (g g' : G)

namespace Action

/-- For f : X.V ‚ü∂ Y.V, compose on the right by X.œÅ g‚Åª¬π : X.V ‚ü∂ X.V -/
def pre_comp : (ihom X.V).obj Y.V ‚ü∂ (ihom X.V).obj Y.V :=
  (monoidal_closed.internal_hom.map (X.œÅ (g‚Åª¬π : G)).op).app Y.V

@[simp] lemma pre_comp_apply :
  pre_comp X Y g = (monoidal_closed.internal_hom.map (X.œÅ (g‚Åª¬π : G)).op).app Y.V := rfl

@[simp] lemma pre_comp_mul :
  pre_comp X Y (g * g') = pre_comp X Y g' ‚â´ pre_comp X Y g := by simp

/-- For f : X.V ‚ü∂ Y.V, compose on the left by Y.œÅ g : Y.V ‚ü∂ Y.V -/
def post_comp : (ihom X.V).obj Y.V ‚ü∂ (ihom X.V).obj Y.V :=
  (monoidal_closed.internal_hom.flip.map (Y.œÅ g)).app (op X.V)

@[simp] lemma post_comp_apply :
  post_comp X Y g = (monoidal_closed.internal_hom.flip.map (Y.œÅ g)).app (op X.V) := rfl

@[simp] lemma post_comp_mul :
  post_comp X Y (g * g') = post_comp X Y g' ‚â´ post_comp X Y g := by simp

lemma pre_post_comp_comm :
  pre_comp X Y g ‚â´ post_comp X Y g' = post_comp X Y g' ‚â´ pre_comp X Y g := by simp

/-- The internal hom of `X Y : Action V G`, i.e. the natural G-action on `[X, Y]` -/
@[protected] def ihom (X Y : Action V G) : Action V G :=
‚ü®(ihom X.V).obj Y.V,
begin
  refine ‚ü®Œª g, pre_comp X Y g ‚â´ post_comp X Y g, _, _‚ü©,
  { simp only [pre_comp_apply, inv_one, œÅ_one, op_id, category_theory.functor.map_id,
      nat_trans.id_app, post_comp_apply, End.one_def],
    apply category.id_comp },
  { intros g g',
    rw [pre_comp_mul, post_comp_mul, End.mul_def, category.assoc, category.assoc],
    nth_rewrite 1 ‚Üêcategory.assoc,
    rw pre_post_comp_comm,
    rw category.assoc }
end‚ü©

@[simp] lemma ihom_V : (ihom X Y).V = (ihom X.V).obj Y.V := rfl

@[simp] lemma ihom_œÅ : (ihom X Y).œÅ g = pre_comp X Y g ‚â´ post_comp X Y g := rfl

/-- `Action.ihom` as a functor -/
def ihom_functor (X : (Action V G)·µí·µñ) : Action V G ‚•§ Action V G :=
{ obj := ihom (unop X),
  map := Œª Y Z f, begin
    refine ‚ü®(monoidal_closed.internal_hom.flip.map f.hom).app (op (unop X).V), _‚ü©,
    { intro,
      simp only [ihom_œÅ, pre_comp_apply, post_comp_apply, functor.flip_map_app, category.assoc,
        nat_trans.naturality_assoc],
      apply congr_arg,
      rw [‚Üêfunctor.map_comp, ‚Üêfunctor.map_comp],
      apply congr_arg,
      apply f.comm },
  end,
  map_id' := by { intro Y, ext, simp only
    [Action.id_hom, category_theory.nat_trans.id_app, category_theory.functor.map_id], congr },
  map_comp' := by { intros, apply Action.hom.ext, simp } }

@[simp] lemma ihom_functor_obj (X : (Action V G)·µí·µñ) :
  (ihom_functor X).obj Y = ihom (unop X) Y := rfl

@[simp] lemma ihom_functor_map_hom (X' : (Action V G)·µí·µñ) (f : X ‚ü∂ Y) :
  ((ihom_functor X').map f).hom =
  (monoidal_closed.internal_hom.flip.map f.hom).app (op (unop X').V) := rfl

@[simp] lemma internal_hom_obj (X : V·µí·µñ) :
  monoidal_closed.internal_hom.obj X = ihom (unop X) := rfl

@[simp] lemma internal_hom_map {X Y : V·µí·µñ} (f : X ‚ü∂ Y) :
  monoidal_closed.internal_hom.map f = monoidal_closed.pre f.unop := rfl

@[simp] lemma internal_hom_obj_map (X : V·µí·µñ) {Y Z : V} (f : Y ‚ü∂ Z) :
  (monoidal_closed.internal_hom.obj X).map f = (ihom (unop X)).map f := rfl

variables {X Y Z}
lemma tensor_left_g_id_comp_injective {V : Type (u+1)} [large_category V] [monoidal_category V]
  {G : Group.{u}} {X Y Z : Action V G} (g : G) {f' f'' : X.V ‚äó Y.V ‚ü∂ Z.V} :
  ((X.œÅ_Aut g).hom ‚äó ùüô Y.V) ‚â´ f' = ((X.œÅ_Aut g).hom ‚äó ùüô Y.V) ‚â´ f'' ‚Üí f' = f'' :=
begin
  intro h,
  have h' := congr_arg (category_struct.comp ((X.œÅ_Aut g).inv ‚äó ùüô Y.V)) h,
  rw [monoidal_category.inv_hom_id_tensor_assoc, monoidal_category.inv_hom_id_tensor_assoc] at h',
  simp only [category_theory.category.id_comp, category_theory.monoidal_category.tensor_id] at h',
  exact h'
end

/-- Elevate the curry on `V` to an `Action V G` hom. -/
def monoidal_closed_curry
  (f : (monoidal_category.tensor_left X).obj Y ‚ü∂ Z) :
  Y ‚ü∂ (ihom_functor (op X)).obj Z :=
‚ü®monoidal_closed.curry f.hom,
begin
  intro,
  rw ‚Üêmonoidal_closed.curry_natural_left,
  dsimp,
  have h := congr_arg (category_struct.comp ((X.œÅ_Aut g).inv ‚äó ùüô Y.V)) (f.comm g),
  dsimp at h,
  rw [‚ÜêAction.œÅ_Aut_apply_inv, ‚ÜêAction.œÅ_Aut_apply_hom, monoidal_category.inv_hom_id_tensor_assoc,
    monoidal_category.tensor_id_comp_id_tensor_assoc] at h,
  apply (monoidal_closed.curry_eq_iff _ _).mpr,
  rw [h, œÅ_Aut_apply_inv, ‚Üêcategory.assoc, ‚Üêcategory.assoc],
  rw monoidal_closed.uncurry_natural_right
    (monoidal_closed.curry f.hom ‚â´ (monoidal_closed.pre (X.œÅ (g‚Åª¬π : G))).app Z.V),
  apply congr_fun,
  apply congr_arg,
  rw [monoidal_closed.uncurry_natural_left, monoidal_closed.uncurry_pre],
  rw @monoidal_category.id_tensor_comp_tensor_id_assoc _ _ _ _ ((ihom X.V).obj Z.V),
  apply tensor_left_g_id_comp_injective g,
  rw [‚ÜêAction.œÅ_Aut_apply_inv, monoidal_category.hom_inv_id_tensor_assoc,
    monoidal_category.hom_inv_id_tensor_assoc],
  simp only [category_theory.category.id_comp, category_theory.monoidal_category.tensor_id,
    ‚Üêmonoidal_closed.uncurry_eq, monoidal_closed.uncurry_curry]
end‚ü©

@[simp] lemma monoidal_closed_curry_hom
  (f : (monoidal_category.tensor_left X).obj Y ‚ü∂ Z) :
  (monoidal_closed_curry f).hom = monoidal_closed.curry f.hom := rfl

/-- Elevate the uncurry on `V` to an `Action V G` hom. -/
def monoidal_closed_uncurry
  (f : Y ‚ü∂ ihom X Z) :
  (monoidal_category.tensor_left X).obj Y ‚ü∂ Z :=
‚ü®monoidal_closed.uncurry f.hom,
begin
  intro,
  dsimp,
  apply tensor_left_g_id_comp_injective g‚Åª¬π,
  rw [Action.œÅ_Aut_apply_hom, ‚ÜêAction.œÅ_Aut_apply_inv, ‚ÜêAction.œÅ_Aut_apply_hom,
    ‚Üêcategory.assoc, monoidal_category.inv_hom_id_tensor, monoidal_category.tensor_id,
    category.id_comp, ‚Üêmonoidal_closed.uncurry_natural_left, f.comm, ihom_œÅ],
  simp only [Action.pre_comp_apply, Action.internal_hom_obj_map, quiver.hom.unop_op,
    Action.œÅ_Aut_apply_inv, category_theory.functor.flip_map_app, Action.internal_hom_map,
    Action.post_comp_apply],
  dsimp,
  rw [‚Üêcategory.assoc, monoidal_closed.uncurry_natural_right, ‚Üêcategory.assoc],
  apply congr_fun,
  apply congr_arg,
  rw [monoidal_closed.uncurry_natural_left, monoidal_closed.uncurry_pre,
    monoidal_category.id_tensor_comp_tensor_id_assoc],
  apply tensor_left_g_id_comp_injective g,
  rw [‚ÜêAction.œÅ_Aut_apply_inv, monoidal_category.hom_inv_id_tensor_assoc,
    monoidal_category.hom_inv_id_tensor_assoc, monoidal_category.tensor_id, category.id_comp,
    category.id_comp, ‚Üêmonoidal_category.tensor_id, ‚Üêmonoidal_closed.uncurry_natural_left,
    ‚Üêmonoidal_closed.curry_eq_iff, ‚Üêmonoidal_closed.uncurry_eq, monoidal_closed.curry_uncurry,
    category.id_comp]
end‚ü©

@[simp] lemma monoidal_closed_uncurry_hom
  (f : Y ‚ü∂ ihom X Z) :
  (monoidal_closed_uncurry f).hom = monoidal_closed.uncurry f.hom := rfl

/-- Intermediate step to constructing `monoidal_closed` -/
def monoidal_closed_hom_equiv :
  ((monoidal_category.tensor_left X).obj Y ‚ü∂ Z) ‚âÉ
  (Y ‚ü∂ (ihom_functor (op X)).obj Z) :=
{ to_fun := monoidal_closed_curry,
  inv_fun := monoidal_closed_uncurry,
  left_inv := by { intro f, apply Action.hom.ext, simp },
  right_inv := by { intro f, apply Action.hom.ext, simp } }

@[simp] lemma monoidal_closed_hom_equiv_apply
  (f : (monoidal_category.tensor_left X).obj Y ‚ü∂ Z) :
  monoidal_closed_hom_equiv f = monoidal_closed_curry f := rfl

@[simp] lemma monoidal_closed_hom_equiv_symm_apply
  (f : Y ‚ü∂ ihom X Z) :
  monoidal_closed_hom_equiv.symm f = monoidal_closed_uncurry f := rfl

/-- For a group `G`, if `V` is a closed monoidal category, then `Action V G` is a closed monoidal
category. -/
instance : monoidal_closed (Action V G) :=
-- ‚ü®Œª X, ‚ü®‚ü®_, _‚ü©‚ü©‚ü©
{ closed' := Œª X,
  { is_adj :=
    { right := ihom_functor (op X),
      adj := begin
        apply adjunction.mk_of_hom_equiv,
        refine ‚ü®Œª Y Z, monoidal_closed_hom_equiv, _, _‚ü©,
        { intros Y Y' Z f f',
          apply Action.hom.ext,
          simp,
          rw ‚Üêmonoidal_closed.uncurry_natural_left },
        { intros Y Z' Z f f',
          apply Action.hom.ext,
          simp,
          dsimp,
          rw monoidal_closed.curry_natural_right }
      end }}}

end Action
